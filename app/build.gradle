apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.alancamargo.tweetreader"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", '"https://api.twitter.com"'
        buildConfigField "String", "CONSUMER_KEY", 'System.getenv("TWITTER_CONSUMER_KEY")'
        buildConfigField "String", "CONSUMER_SECRET", 'System.getenv("TWITTER_CONSUMER_SECRET")'
        buildConfigField "String", "GRANT_TYPE", '"client_credentials"'
    }

    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file(System.getenv("STORE_FILE"))
            storePassword System.getenv("STORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        bolsonaro {
            dimension "version"
            applicationIdSuffix ".bolsonaro"
            buildConfigField "String", "USER_ID", '"128372940"'
        }

        joaquinTeixeira {
            dimension "version"
            applicationIdSuffix ".joaquinteixeira"
            buildConfigField "String", "USER_ID", '"944656536940531713"'
        }

        paulJosephWatson {
            dimension "version"
            applicationIdSuffix ".pauljosephwatson"
            buildConfigField "String", "USER_ID", '"18643437"'
        }
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // region Main code libraries
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    kapt "androidx.room:room-compiler:$room_version"
    // endregion

    // region Instrumented test libraries
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
    // endregion

    // region Unit test libraries
    testImplementation 'junit:junit:4.12'
    // endregion
}
